ALTER PROC [dbo].[SP_CHUYENTIEN] (@STKGUI nChar(9), @STKNHAN nChar(9), @TIEN MONEY,@MANV NCHAR(10))
AS
SET XACT_ABORT ON	--NEU OFF THI SQL SE BO QUA LENH GAY LOI VA CHAY TIEP
BEGIN
	--KIEM TRA STK_GUI VA STK_NHAN CO TON TAI TRONG DB HAY KHONG, SAU DO THI KIEM TRA SO DU NGUOI GUI.
	IF ( EXISTS( SELECT SOTK FROM TaiKhoan WHERE SOTK=@STKNHAN ) AND 
			EXISTS( SELECT SOTK FROM TaiKhoan WHERE SOTK=@STKGUI) )
		BEGIN
			DECLARE @SODU_NGUOICHUYEN MONEY
			SELECT @SODU_NGUOICHUYEN = SODU FROM TaiKhoan WHERE @STKGUI = SOTK

			IF @SODU_NGUOICHUYEN < @TIEN
				RAISERROR('SO DU KHONG DU !!!', 16, 1)
			ELSE
				BEGIN
					BEGIN TRANSACTION
					BEGIN TRY
						UPDATE TaiKhoan
						SET SODU -= @TIEN
						WHERE @STKGUI = SOTK

						UPDATE TaiKhoan
						SET SODU += @TIEN
						WHERE @STKNHAN = SOTK

						INSERT INTO GD_CHUYENTIEN(SOTK_CHUYEN,NGAYGD,SOTIEN,SOTK_NHAN,MANV) 
							VALUES (@STKGUI,GETDATE(),@TIEN,@STKNHAN,@MANV)

						COMMIT
					END TRY
					
					BEGIN CATCH		-- một số trường hợp bất ngờ có thể xảy ra gây ra lỗi
						ROLLBACK	-- ví dụ như mất điện khi đang cộng trừ tiền trong tài khoản.
						DECLARE @ERRORMESSAGE VARCHAR(2000)
						SELECT @ERRORMESSAGE = 'Lỗi: ' + ERROR_MESSAGE()
						RAISERROR(@ERRORMESSAGE, 16, 1)
					END CATCH
				END
		END
	ELSE
		RAISERROR('SO TAI KHOAN KHONG TON TAI !!!', 16, 1)
END






ALTER PROC [dbo].[SP_GUIRUT] (@STK	NCHAR(9), @TIEN MONEY, @LOAIGD NCHAR(2),@MANV NCHAR(10))
AS
SET XACT_ABORT ON		-- NEU OFF THI SQL SE BO QUA LENH GAY LOI.
BEGIN TRANSACTION		-- Nếu chỉ SP chỉ có một lệnh UPDATE thì ta không cần sử dụng giao tác vì nó là giao tác tự động.
	BEGIN TRY			-- Nhưng SP này ta có 2 lệnh là UPDATE và INSERT.
		IF EXISTS(SELECT SOTK FROM TaiKhoan WHERE SOTK=@STK)	-- Kiểm tra stk có tồn tại hay không, sau đó xét xem nó là 'RT' hay 'GT'
			BEGIN
				DECLARE @SODU MONEY
				SELECT @SODU=SODU FROM TaiKhoan WHERE @STK=SOTK

				IF @LOAIGD = 'GT'
					BEGIN
						UPDATE TaiKhoan
						SET SODU += @TIEN
						WHERE @STK = SOTK

						INSERT INTO GD_GOIRUT(SOTK,LOAIGD,NGAYGD,SOTIEN,MANV) 
							VALUES(@STK,@LOAIGD,GETDATE(),@TIEN,@MANV)
					END

	
				ELSE IF @LOAIGD = 'RT'
					BEGIN
						IF @SODU < @TIEN
							RAISERROR('SO DU KHONG DU !!!', 16, 1)
						ELSE
							BEGIN
								UPDATE TaiKhoan
								SET SODU -= @TIEN
								WHERE @STK = SOTK

								INSERT INTO GD_GOIRUT(SOTK,LOAIGD,NGAYGD,SOTIEN,MANV) 
									VALUES(@STK,@LOAIGD,GETDATE(),@TIEN,@MANV)
							END
					END
			END
		ELSE
			RAISERROR('SO TAI KHOAN KHONG TON TAI !!!', 16, 1)
		
		COMMIT
	END TRY
	
BEGIN CATCH
	ROLLBACK
	DECLARE @ERRORMESSAGE VARCHAR(2000)
	SELECT @ERRORMESSAGE = 'Lỗi: ' + ERROR_MESSAGE()
	RAISERROR(@ERRORMESSAGE, 16, 1)
END CATCH










ALTER PROC [dbo].[SP_Lay_Thong_Tin_NV_Tu_Login] @TENLOGIN NVARCHAR(20)
AS
BEGIN
	DECLARE @UID INT
	DECLARE @MANV INT

	SELECT @UID = UID, @MANV = NAME  FROM SYS.sysusers
		WHERE SID = SUSER_SID(@TENLOGIN)

	

	SELECT  
		 MANV = @MANV, HOVATEN = (SELECT (HO + ' ' + TEN) FROM NHANVIEN WHERE @MANV = MANV),
		NHOM = NAME FROM SYS.SYSUSERS 
			WHERE UID = (SELECT GROUPUID FROM SYS.SYSMEMBERS WHERE MEMBERUID = @UID)
END



ALTER PROC [dbo].[SP_TimNV_tu_MANV]
@X INT
AS
DECLARE @MACN VARCHAR(10), @HO NVARCHAR(20), @TEN NVARCHAR(10)
IF EXISTS (SELECT MANV FROM DBO.NHANVIEN WHERE MANV=@X)		/* Tim trong phan manh hien tai */
	BEGIN
		SELECT TENCN = (SELECT TENCN FROM DBO.CHINHANH), HO, TEN 
		FROM DBO.NHANVIEN WHERE MANV=@X
	END
ELSE
	IF EXISTS(SELECT MANV FROM LINK0.NGANHANG.DBO.NHANVIEN WHERE MANV=@X)	/* Tim trong site chu */
	BEGIN
		SELECT @MACN=MACN, @HO=HO, @TEN=TEN FROM LINK0.NGANHANG.DBO.NHANVIEN
		WHERE MANV=@X
		SELECT TENCN, HO=@HO, TEN=@TEN FROM LINK0.NGANHANG.DBO.CHINHANH WHERE @MACN=MACN
	END
	ELSE	/* Khong tim thay */
	RAISERROR('Ma nhan vien khong ton tai!!!', 16, 1)










